#!/usr/bin/purc

# RESULT: 'Good Client'

<hvml target="void">
    <head>
        <define as 'logMsg'>
            $RUNNER.enablelog('all', 'stderr')

            <inherit>
                $RUNNER.logmsg($?)
            </inherit>
        </define>
    </head>

    <body id='server' >

        <execute with $logMsg on 'The server is running...' />
        <execute with $logMsg on "The server load module : $REQ.module" />

        <init as varDefOnMain with "var define on server" />

        <define as "wsModule" from "$REQ.module" />

        <init as 'sslSettings' with {} />
        <test with $L.streq('caseless', $REQ.secure, 'true') >
            <update on $sslSettings to 'merge'>
                {
                    'sslkey': "$REQ.base/localhost.key",
                    'sslcert': "$REQ.base/localhost.crt",
                }
            </update>
        </test>

        <init as 'wsSettings' with {} />
        <test with $STR.contains($REQ.client, 'ltnr') >
            <update on $wsSettings to 'merge'>
                {
                    'noresptimetoping': 3,
                    'noresptimetoclose': 6,
                }
            </update>
        </test>

        <test with $L.streq('caseless', $REQ.client, 'tlmsg') >
            <update on $wsSettings to 'merge'>
                {
                    'maxmessagesize': 4096,
                }
            </update>
        </test>

        <execute with $logMsg on $DATA.serialize($sslSettings) />
        <execute with $logMsg on $DATA.serialize($wsSettings) />

        <init as 'clients' with {} />

        <init as 'srvListenSocket' with $SOCKET.stream('inet://localhost:8080', 'none', 32, $sslSettings) >
            <catch for `ANY`>
                <exit with "Server failed with $?.name when calling SOCKET.stream()" />
            </catch>
        </init>

        <load from "#plain" within 'client' onto 'null:' as 'client' async >
            <catch for `ANY`>
                <exit with "Server failed with $?.name when loading client" />
            </catch>
        </load>

        <observe on $client for "corState:exited" >
            <inherit>
                $STREAM.stdout.writelines("Client exited with $?")
            </inherit>
        </observe>

        <execute with $logMsg on 'The server is accepting...' />

        <choose on $srvListenSocket.accept('default', 'websocket', $wsSettings)>
            <catch for `ANY`>
                <exit with "Server failed with $?.name when calling accept()" />
            </catch>

            <test with $L.not($DATA.isequal($?, null)) >

                <execute with $logMsg on "Server accpeted a client from $?.peerAddr:$?.peerPort" />

                <init as 'clientId' with "$?.peerAddr:$?.peerPort" temp />

                <update on $clients to 'merge' with { $clientId : $? } />

                <observe on $clients[$clientId] for 'handshake'>
                    $clientId

                    <inherit>
                    {{
                        $STREAM.stdout.writelines("Server got HANDSHAKE event from `$_observedContent`; payload:");
                        $STREAM.stdout.writelines($DATA.serialize($?));
                        $_observedOn.send_handshake_resp(101);
                    }}
                    </inherit>
                    <execute with $wsModule />

                    <!-- test access variable   -->
                    <execute with $logMsg on "The server access variable define on module \$varDefOnModule : $varDefOnModule" />
                    <execute with $logMsg on "The server access variable define on module \$varDefOnModuleBindById : $varDefOnModuleBindById" />

                </observe>

            </test>
        </choose>

    </body>

    <!-- A plain client -->
    <body id="plain">

        <execute with $logMsg on 'Client is running...' />

        <init as 'cliStreamSocket' with $STREAM.open('inet://localhost:8080/', 'default', 'websocket', { secure: false } ) >
            <catch for `ANY`>
                <exit with "Client failed with $?.name when calling STREAM.open()" />
            </catch>

            <execute with $logMsg on 'Client has connected to the server.' />

        </init>

        <observe on $cliStreamSocket for 'handshake'>
            <execute with $logMsg on 'Client got HANDKSHAKE event' />
            <execute with $logMsg on $DATA.serialize($?) />

            <execute with $logMsg on 'Client is going to send some words.' />

            <inherit>
                {{
                     $cliStreamSocket.send("Hello from a good client.");
                 }}

                <catch for `ANY`>
                    <exit with "Client failed with $?.name when calling cliStreamSocket.close()" />
                </catch>
            </inherit>
        </observe>

        <observe on $cliStreamSocket for 'message'>
            <execute with $logMsg on 'Client got MESSAGE event from the server:' />
            <execute with $logMsg on $DATA.serialize($?) />

            <execute with $logMsg on 'Client is going to say bye.' />

            <inherit>
                {{
                     $cliStreamSocket.send("Bye");
                 }}

                <catch for `ANY`>
                    <exit with "Client failed with $?.name when calling cliStreamSocket.close()" />
                </catch>
            </inherit>

        </observe>

        <observe on $cliStreamSocket for 'close'>
            <execute with $logMsg on 'Client got CLOSE event from the server:' />
            <execute with $logMsg on $DATA.serialize($?) />

            <execute with $logMsg on 'Client is going to exit.' />

            <inherit>
                {{
                     $cliStreamSocket.close()
                 }}

                <catch for `ANY`>
                    <exit with "Client failed with $?.name when calling cliStreamSocket.close()" />
                </catch>
            </inherit>

            <exit with 'Bye' />
        </observe>

        <observe on $cliStreamSocket for 'error'>
            <execute with $logMsg on 'Client got ERROR event from the server:' />
            <execute with $logMsg on $DATA.serialize($?) />

            <execute with $logMsg on 'Client is going to exit.' />

            <inherit>
                {{
                     $cliStreamSocket.close()
                 }}

                <catch for `ANY`>
                    <exit with "Client failed with $?.name when calling cliStreamSocket.close()" />
                </catch>
            </inherit>

            <exit with 'Bye' />
        </observe>
    </body>

</hvml>

