#!/usr/bin/purc

# RESULT: { 'client0': bx00112233445566778899AABBCC, 'client1': bx112233445566778899AABBCCDD, 'client2': bx2233445566778899AABBCCDDEE, 'client3': bx33445566778899AABBCCDDEEFF }


<hvml target="void">
    <body id='result' >

        <init as 'got' with { } />

        <init as 'dgramSocket' with $SOCKET.dgram('local:///var/tmp/hvml-dgram-server', 'default') >
            <catch for `ANY`>
                <exit with "Server failed with $?.name when calling SOCKET.dgram()" />
            </catch>
            <observe on $dgramSocket for 'socket:newDatagram' >
                <inherit>
                    $STREAM.stdout.writelines('Got socket:newDatagram event')
                </inherit>

                <choose on $dgramSocket.recvfrom('dontwait', 13) >
                    <inherit>
                        $STREAM.stdout.writelines("Return values of recvfrom(): $?.recved / $?.errorname")
                    </inherit>

                    <test with $L.gt($?.recved, 0) >
                        <init as 'clientName' with $STR.strstr($?['source-addr'], 'client') temp />

                        <update on $got at ".$clientName" with $DATA.pack("bytes:0 bytes:{$?.recved}", $got[$clientName], $?.bytes) />

                        <differ>
                            $STREAM.stdout.writelines("Do nothing")
                        </differ>
                    </test>

                    <catch for `ANY`>
                        <exit with "Server failed with $?.name when calling dgramSocket.recvfrom()" />
                    </catch>
                </choose>
            </observe>
        </init>

        <define as 'sendDatagram' >
            <init as 'dgramSocket' with $SOCKET.dgram('local:///var/tmp/hvml-dgram-$?.name', 'default') >
                <catch for `ANY`>
                    <return with "Client $2?.name failed with $?.name when calling SOCKET.dgram()" />
                </catch>
            </init>

            <choose on $dgramSocket.sendto('local:///var/tmp/hvml-dgram-server', 'default', $?.bytes) >
                <inherit>
                    $STREAM.stdout.writelines("Client $2?.name sent bytes: $?.sent")
                </inherit>

                <return with 'success' />

                <catch for `ANY`>
                    <return with "Client $2?.name failed with $?.name when calling dgramSocket.sendto()" />
                </catch>
            </choose>
        </define>

        <init as 'datagrams'>
            {
                'client0': bx00112233445566778899AABBCC,
                'client1': bx112233445566778899AABBCCDD,
                'client2': bx2233445566778899AABBCCDDEE,
                'client3': bx33445566778899AABBCCDDEEFF
            }
        </init>

        <init as 'status'>
            {
                'success': 0,
                'error': 0,
                'except': 0,
            }
        </init>

        <iterate on $datagrams by 'KEY: ALL FOR KV' >

            <inherit>
                $STREAM.stdout.writelines("Calling sendDatagram for $?.k")
            </inherit>

            <call on $sendDatagram within "$?.k" with {name: $?.k, bytes: $?.v} as "$?.k" concurrently asynchronously />
            <observe on ${$?.k} for 'callState:success'>
                <inherit>
                    $STREAM.stdout.writelines("Client returns: $?")
                </inherit>
                <update on $status at '.success' with $DATA.arith('+', $status['success'], 1) />
            </observe>

            <observe on ${$?.k} for 'callState:error/*'>
                <inherit>
                    $STREAM.stdout.writelines("Client returns with an error: $?")
                </inherit>
                <update on $status at '.error' with $DATA.arith('+', $status['error'], 1) />
            </observe>

            <observe on ${$?.k} for 'callState:except/*'>
                <inherit>
                    $STREAM.stdout.writelines("Client returns with an exception: $?")
                </inherit>
                <update on $status at '.except' with $DATA.arith('+', $status['except'], 1) />
            </observe>
        </iterate>

        <observe on $CRTN for 'idle'>

            <test with $L.eq($DATA.arith('+', $status['success'], $DATA.arith('+', $status['error'], $status['except'])), 4) >
                <exit with $got />
            </test>

        </observe>

    </body>

</hvml>

